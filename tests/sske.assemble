* Set storage key extended test case.                                   00010000
                                                                                
* This file was put into the public domain 2015-10-05                   00020000
* by John P. Hartmann.  You can use it for anything you like,           00030000
* as long as this notice remains.                                       00040000
                                                                                
* We load code at x'200' unless prefixing is indicated, in which                
* case we load at x'2000'                                                       
                                                                                
 macro                                                                          
 supsws &problem=0,&prefix=no                                                   
&havepx setb ('&prefix' eq 'yes')                                               
&org(1) setc 'x''200''','x''2000'''                                             
 org sske+x'1a0'                                                        00090000
 dc x'000&problem.000180000000'                                         00100000
 dc ad(&org(&havepx+1))                                                 00110000
 org sske+x'1d0'                                                        00120000
 dc x'0002000180000000'                                                 00130000
 dc ad(x'deaddead') Program new PSW                                     00140000
 aif (&havepx).px                                                               
 org &sysect+x'200'                                                     00150000
 using &sysect,0                                                                
 mexit                                                                          
.px anop                                                                        
 org &sysect+x'41d0'                                                    00120000
 dc x'0002000180000000'                                                 00130000
 dc ad(x'deadbeaf') Program new PSW in prefix                           00140000
 org &sysect+x'2000' Get out of the prefix page                                 
 drop 0                                                                         
 basr 12,0                                                                      
 using *,12                                                                     
 spx =a(x'4000') Load prefix                                                    
.x mend                                                                         
                                                                                
 macro                                                                          
 stop &rc=0                                                                     
 lpswe psw&sysndx                                                               
psw&sysndx dc 0d,x'0002000180000000',ad(&rc)                                    
 ltorg                                                                          
 mend                                                                           
                                                                                
* Test for instruction being privileged                                         
                                                                                
 punch ':test sske#1'                                                   00050000
 punch ':btst z'                                                        00060000
sske start 0                                                            00070000
 supsws problem=1                                                               
go ds 0h =x'200'                                                                
 lhi 2,x'2004'  Starting address                                        00170000
 lhi 3,x'f0'                                                            00180000
 sske 3,2                                                               00190000
 stop rc=x'bad'                                                         00200000
 punch '* *Program 0002' Expect program check                           00240000
 punch ':etst 1'                                                        00250000
 punch '* *Done'                                                        00260000
                                                                                
* Test ability to set a key                                                     
 punch ':test sske#2'                                                   00050000
 punch ':btst z'                                                        00060000
 supsws                                                                         
 org go                                                                         
 lhi 2,x'2004'                                                                  
 la  3,x'f0'                                                                    
 SSKE    3,2                                                                    
 stop                                                                           
 punch ':etst 1'                                                        00250000
 punch '* *Compare'                                                             
 punch '* r 2000.8'                                                             
 punch '* *Key F0'                                                              
 punch '* gpr'                                                                  
 punch '* *Gpr 2 2004'                                                          
                                                                                
* Test prefixing with single page frame                                         
* Make the fourth page the prefix.  Note that the prefix is two                 
* pages                                                                         
                                                                                
 punch '* * Test prefixing with single page frame.'                             
 punch ':btst z' Reset machine                                          00060000
 supsws prefix=yes                                                              
 lhi 2,x'1004'                                                                  
 la  3,x'f0'                                                                    
 SSKE    3,2                                                                    
 stop                                                                           
 punch ':etst 1'                                                        00250000
 punch '* *Compare'                                                             
 punch '* abs 1000.8'                                                           
 punch '* *Key 00'                                                              
 punch '* r 1000.8'                                                             
 punch '* *Key F0'                                                              
 punch '* abs 5000.8'                                                           
 punch '* *Key F0'                                                              
 punch '* r 5000.8'                                                             
 punch '* *Key 00'                                                              
                                                                                
* Set multiple keys.  Requires 1M page support                                  
                                                                                
* Setting  multiple keys without prefixing.  The low 12 bits of       *         
* R2 should remain unchanged.                                         *         
                                                                                
 punch ':btst z' Reset machine                                          00060000
 supsws                                                                         
 lhi 2,x'2004'  Starting address                                        00170000
 lhi 3,x'f0'                                                            00180000
 sske 3,2,1                                                             00190000
 stop                                                                           
 punch ':etst 1'                                                        00250000
 punch '* *Compare'                                                             
 punch '* r 2000.8' r command displays absolute storage                         
 punch '* *Key F0'                                                              
 punch '* r 5000.8'                                                             
 punch '* *Key F0'                                                              
 punch '* r ff000.8'                                                            
 punch '* *Key F0'                                                              
 punch '* r 100000.8'                                                           
 punch '* *Key 00'                                                              
 punch '* gpr'                                                                  
 punch '* *Gpr 2 100004'                                                        
                                                                                
* Same  with prefixing.  The point being that this variation of       *         
* the instruction is not subject to prefixing,                        *         
                                                                                
 punch '* * Test prefixing with multiple page frames.'                          
 punch ':btst z' Reset machine                                          00060000
 supsws prefix=yes                                                              
 lhi 2,x'1004'  Starting address                                        00170000
 lhi 3,x'f0'                                                            00180000
 sske 3,2,1                                                             00190000
 stop                                                                           
 punch ':etst 1'                                                        00250000
 punch '* *Compare'                                                             
 punch '* abs  0.8' r command displays absolute storage                         
 punch '* *Key 06'                                                              
 punch '* r    0.8' r command displays absolute storage                         
 punch '* *Key f0'                                                              
 punch '* abs 1000.8' r command displays absolute storage                       
 punch '* *Key F0'                                                              
 punch '* r 1000.8' r command displays absolute storage                         
 punch '* *Key F0'                                                              
 punch '* abs 2000.8' r command displays absolute storage                       
 punch '* *Key F4'                                                              
 punch '* r 2000.8' r command displays absolute storage                         
 punch '* *Key F4'                                                              
 punch '* r 5000.8'                                                             
 punch '* *Key F0'                                                              
 punch '* r ff000.8'                                                            
 punch '* *Key F0'                                                              
 punch '* r 100000.8'                                                           
 punch '* *Key 00'                                                              
 punch '* gpr'                                                                  
 punch '* *Gpr 2 100004'                                                        
                                                                                
* Insert more here                                                              
                                                                                
 punch '* *Done'                                                        00260000
 end                                                                    00270000
