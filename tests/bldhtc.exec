/* Build Hyperion test case                                          */
/*                               John Hartmann  22 Sep 2015 13:23:37 */
 
/* This file  was put into the  public domain 2015-10-05 by  John P. */
/* Hartmann.  You can use it for  anything you like, as long as this */
/* notice remains.                                                   */
 
/* Hercules  basic  testing workflow  consists  of  (a) creating  an */
/* assemble file; (b)  assembly with the system  assembler or HLASM, */
/* if you have it; (c) process TEXT deck to obtain Hercules commands */
/* (.tst  extension);  (d)  download  to the  tests  directory;  (e) */
/* execute the commands and write the console log to a file; and (f) */
/* post  processing  this file  to  ensure  that tests  produce  the */
/* correct results.                                                  */
/*                                                                   */
/* This EXEC performs  steps (b) and (c).  It also  writes a listing */
/* file.   It requires  z/VM version  2 or  later and  a recent  CMS */
/* Pipelines.   (vm.marist.edu/%7epipeline).   ASMTST XEDIT  perform */
/* these steps on the file being edited.                             */
/*                                                                   */
/* Step (c) consists of processing the object deck stream to (c1) to */
/* convert TXT  records into Hercules  r commands to store  the text */
/* into real storage; generate multiple  stores for TXT records that */
/* contain more than 16 bytes  of data.  (c2) procecess object cards */
/* that begin  with an asterisk  as o Hercules commands  by deleting */
/* the  asterisk.    Some  comment  cards  control   the  test  post */
/* processing in  step (f).   (c3) Process  object cards  that begin */
/* with  a  colon as  macros  that  generate parameterised  Hercules */
/* commands.                                                         */
/*                                                                   */
/* Step  (e) and  (f) are  performed by  the "runtest"  shell script */
/* (UNIX) or the "runtest.cmd" windows command script.               */
 
Signal on novalue
numeric digits 12
Address COMMAND
 
parse arg fn .
If fn = ''
   Then fn = 'logicimm'
parse source . . myfn myft .
 
'PIPE (end \ name BLDHTC)',
    , /* Read input file and pass to assembler.                      */
   '\<' fn 'assemble h',
   '|h:hlasm xyz asa',
    , /* Process TEXT stream for TXT records                         */
   '|p: pick 1 == x02 and 2+ == /TXT/',
   '|spec a: 6.3 . l: 11.2 . set ( #0:=c2d(a); #1:=c2d(l); #2:=17 )',
   '   while (#1 > 0) do',
   '      /r / 1 print d2x(#0) n.6 r /=/ n print c2x(record(#2, min(#1, 16))) next',
   '      set (#0+=16; #1-=16; #2+=16)',
   '      if (#1 > 0) then write endif',
   '   done',
   '|iw: faninany',
   '|>' fn 'tst h',
   '|brw',
    , /* Process TEXT stream for : macros.                           */
   '\p:',
   '|col: if pick 1 == /:/',
   '|d: deal streamid 2.4',
   '|insert /??? /',
   '|cons',
    , /* Process TEXT  stream for  * indicating Hercules  command in */
    , /* columns 2-72.                                               */
   '|col:',
   '|pick 1 == /*/',
   '|substr 2-*',
   '|strip',
   '|col:',
   '|iw:',
   '\d.test:',
   '|spec /*Testcase/ 1 w2 nw /assembled' date() time() 'by' myfn myft'/ nw',
   '|iw:',
   '\d.btst:',
   '|spec /sysclear/ 1 write /archmode/ 1 w2 nw',
   '|iw:',
   '\d.etst:',
   '|spec /restart/ 1 write /pause/ 1 w2 nw',
   '|iw:',
    , /* Write listing file                                          */
   '\h:',
   '|>' fn 'listing h',
    , /* Extract error messages from the ADATA stream.               */
   '\h:',
   '|hlasmerr',
   '|cons'
 
Exit RC
